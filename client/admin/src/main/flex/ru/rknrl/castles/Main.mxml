<?xml version="1.0"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:mx="library://ns.adobe.com/flex/mx">

    <s:Group id="authGroup" width="100%" height="100%">
        <s:Form horizontalCenter="0" verticalCenter="0">
            <s:FormItem label="Login:">
                <s:TextInput id="loginTextInput" width="300"/>
            </s:FormItem>
            <s:FormItem label="Password:">
                <s:TextInput id="passwordTextInput" width="300"/>
            </s:FormItem>
            <s:FormItem label="Host:">
                <s:TextInput id="hostTextInput" text="127.0.0.1" width="300"/>
            </s:FormItem>
            <s:FormItem>
                <s:Button label="Connect" click="onConnectClick(event)"/>
            </s:FormItem>
        </s:Form>
    </s:Group>

    <s:Group id="loadingGroup" width="100%" height="100%"
             includeInLayout="false" visible="false">
        <s:Label text="Loading" horizontalCenter="0" verticalCenter="0"/>
    </s:Group>

    <mx:TabNavigator id="adminGroup"
                     includeInLayout="false" visible="false"
                     width="100%" height="100%">


        <mx:HBox label="Graph" width="100%" height="100%">
            <mx:LineChart id="chart" width="100%" height="100%" showDataTips="true">

                <mx:seriesFilters>
                    <fx:Array/>
                </mx:seriesFilters>

                <mx:series>
                    <mx:LineSeries yField="online" form="segment" displayName="Online"/>
                    <mx:LineSeries yField="games" form="segment" displayName="Games"/>
                    <mx:LineSeries yField="totalMem" form="segment" displayName="Mem (Mb)"/>
                </mx:series>

                <mx:verticalAxis>
                    <mx:LinearAxis/>
                </mx:verticalAxis>

                <mx:horizontalAxis>
                    <mx:LinearAxis/>
                </mx:horizontalAxis>

            </mx:LineChart>

            <mx:Legend dataProvider="{chart}"/>
        </mx:HBox>

        <mx:Box label="Admin" width="100%" height="100%">
            <s:Scroller width="100%" height="100%"
                        creationComplete="onCreationComplete(event)">

                <s:VGroup width="100%">
                    <s:HGroup>
                        <s:DropDownList id="accountTypeComboBox"
                                        dataProvider="{Utils.accountTypes}"
                                        labelFunction="{Utils.enumLabelFunction}"
                                        selectedIndex="1"/>

                        <s:TextInput id="accountIdInput"/>

                        <s:Button label="Get" click="onGetClick(event)"/>
                    </s:HGroup>

                    <s:Spacer height="50"/>

                    <s:VGroup id="stateHolder"/>

                    <s:Button label="Delete Account" click="onDeleteClick(event)"/>

                </s:VGroup>
            </s:Scroller>
        </mx:Box>

    </mx:TabNavigator>

    <fx:Script><![CDATA[
        import mx.controls.Alert;
        import mx.events.FlexEvent;

        import ru.rknrl.castles.events.AddBalanceEvent;
        import ru.rknrl.castles.events.AddMagicItemEvent;
        import ru.rknrl.castles.events.SetSkillEvent;
        import ru.rknrl.castles.events.SetSlotEvent;
        import ru.rknrl.dto.AccountIdDTO;
        import ru.rknrl.dto.AccountStateDTO;
        import ru.rknrl.dto.AdminAddGoldDTO;
        import ru.rknrl.dto.AdminAddItemDTO;
        import ru.rknrl.dto.AdminAuthenticateDTO;
        import ru.rknrl.dto.AdminGetAccountStateDTO;
        import ru.rknrl.dto.AdminSetSkillDTO;
        import ru.rknrl.dto.AdminSetSlotDTO;
        import ru.rknrl.dto.ItemDTO;
        import ru.rknrl.dto.ServerHealthItemDTO;
        import ru.rknrl.dto.SkillLevelDTO;
        import ru.rknrl.dto.SlotDTO;
        import ru.rknrl.rmi.AccountStateEvent;
        import ru.rknrl.rmi.AuthenticatedAsAdminEvent;
        import ru.rknrl.rmi.Server;
        import ru.rknrl.rmi.ServerHealthEvent;

        private static const port:int = 2337;
        private static const policyPort:int = 2336;

        private var server:Server;
        private var getHealthInterval:int;

        private function onCreationComplete(event:FlexEvent):void {
            stateHolder.addEventListener(AddBalanceEvent.ADD_BALANCE, onAddBalance);
            stateHolder.addEventListener(AddMagicItemEvent.ADD_MAGIC_ITEM, onAddMagicItem);
            stateHolder.addEventListener(SetSkillEvent.SET_SKILL, onSetSkill);
            stateHolder.addEventListener(SetSlotEvent.SET_SLOT, onSetSlot);
        }

        private function onConnectClick(event:MouseEvent):void {
            authGroup.includeInLayout = authGroup.visible = false;
            loadingGroup.includeInLayout = loadingGroup.visible = true;
            createConnection(hostTextInput.text, port);
        }

        private function createConnection(host:String, port:int):void {
            if (server) throw new Error("already connected");

            Security.loadPolicyFile("xmlsocket://" + host + ":" + policyPort);

            server = new Server(new Socket());
            server.addEventListener(Event.CONNECT, onConnect);
            server.addEventListener(SecurityErrorEvent.SECURITY_ERROR, onConnectionError);
            server.addEventListener(IOErrorEvent.IO_ERROR, onConnectionError);
            server.addEventListener(Event.CLOSE, onConnectionError);
            server.connect(host, port);
        }

        private function onConnectionError(event:Event):void {
            Alert.show(event.toString());
        }

        private function onConnect(event:Event):void {
            const authenticate:AdminAuthenticateDTO = new AdminAuthenticateDTO();
            authenticate.login = loginTextInput.text;
            authenticate.password = passwordTextInput.text;
            server.addEventListener(AuthenticatedAsAdminEvent.AUTHENTICATEDASADMIN, onAuthenticatedAsAdmin);
            server.authenticateAsAdmin(authenticate);
        }

        public function onAuthenticatedAsAdmin(e:AuthenticatedAsAdminEvent):void {
            server.removeEventListener(AuthenticatedAsAdminEvent.AUTHENTICATEDASADMIN, onAuthenticatedAsAdmin);
            loadingGroup.includeInLayout = loadingGroup.visible = false;
            adminGroup.includeInLayout = adminGroup.visible = true;
            server.addEventListener(AccountStateEvent.ACCOUNTSTATE, onAccountState);
            server.addEventListener(ServerHealthEvent.SERVERHEALTH, onServerHealth);

            getHealthInterval = setInterval(server.getServerHealth, 1000);
            server.getServerHealth();
        }

        private function onGetClick(event:MouseEvent):void {
            stateHolder.removeAllElements();

            const dto:AdminGetAccountStateDTO = new AdminGetAccountStateDTO();
            dto.accountId = new AccountIdDTO();
            dto.accountId.id = accountIdInput.text;
            dto.accountId.type = accountTypeComboBox.selectedItem;
            server.getAccountState(dto);
        }

        private var currentAccountId:AccountIdDTO;

        public function onAccountState(e:AccountStateEvent):void {
            stateHolder.removeAllElements();

            const state:AccountStateDTO = e.adminAccountState.accountState;

            currentAccountId = e.adminAccountState.accountId;

            const nameRow:NameRow = new NameRow();
            nameRow.init(currentAccountId);
            stateHolder.addElement(nameRow);

            stateHolder.addElement(spacer);

            const goldRow:BalanceRow = new BalanceRow();
            goldRow.init(state.gold);
            stateHolder.addElement(goldRow);

            stateHolder.addElement(spacer);

            for each(var slot:SlotDTO in state.slots) {
                const slotRow:SlotRow = new SlotRow();
                slotRow.init(slot.id, slot.buildingPrototype);
                stateHolder.addElement(slotRow);
            }

            stateHolder.addElement(spacer);

            for each(var skill:SkillLevelDTO in state.skills) {
                const skillRow:SkillRow = new SkillRow();
                skillRow.init(skill.type, skill.level);
                stateHolder.addElement(skillRow);
            }

            stateHolder.addElement(spacer);

            for each(var item:ItemDTO in state.items) {
                const magicItemRow:MagicItemRow = new MagicItemRow();
                magicItemRow.init(item.type, item.count);
                stateHolder.addElement(magicItemRow);
            }
        }

        private static function get spacer():Spacer {
            const spacer:Spacer = new Spacer();
            spacer.height = 16;
            return spacer;
        }

        private function onAddBalance(event:AddBalanceEvent):void {
            const dto:AdminAddGoldDTO = new AdminAddGoldDTO();
            dto.accountId = currentAccountId;
            dto.amount = event.amount;
            server.addGold(dto)
        }

        private function onAddMagicItem(event:AddMagicItemEvent):void {
            const dto:AdminAddItemDTO = new AdminAddItemDTO();
            dto.accountId = currentAccountId;
            dto.itemType = event.itemType;
            dto.amount = event.amount;
            server.addItem(dto)
        }

        private function onSetSlot(event:SetSlotEvent):void {
            const dto:AdminSetSlotDTO = new AdminSetSlotDTO();
            dto.accountId = currentAccountId;
            dto.slot = event.slotDto;
            server.setSlot(dto)
        }

        private function onSetSkill(event:SetSkillEvent):void {
            const dto:AdminSetSkillDTO = new AdminSetSkillDTO();
            dto.accountId = currentAccountId;
            dto.skilType = event.skillType;
            dto.skillLevel = event.skillLevel;
            server.setSkill(dto)
        }

        private function onServerHealth(event:ServerHealthEvent):void {
            const arr:Array = [];
            for each(var item:ServerHealthItemDTO in event.serverHealth.items) arr.push(item);
            chart.dataProvider = arr;
        }

        private function onDeleteClick(event:MouseEvent):void {
            server.deleteAccount(currentAccountId);
        }
        ]]></fx:Script>
</s:Application>
