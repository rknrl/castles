<?xml version="1.0"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark">

    <s:Group id="authGroup" width="100%" height="100%">
        <s:Form horizontalCenter="0" verticalCenter="0">
            <s:FormItem label="Login:">
                <s:TextInput id="loginTextInput" width="300"/>
            </s:FormItem>
            <s:FormItem label="Password:">
                <s:TextInput id="passwordTextInput" width="300"/>
            </s:FormItem>
            <s:FormItem label="Host:">
                <s:TextInput id="hostTextInput" text="127.0.0.1" width="300"/>
            </s:FormItem>
            <s:FormItem>
                <s:Button label="Connect" click="onConnectClick(event)"/>
            </s:FormItem>
        </s:Form>
    </s:Group>

    <s:Group id="loadingGroup" width="100%" height="100%"
             includeInLayout="false" visible="false">
        <s:Label text="Loading" horizontalCenter="0" verticalCenter="0"/>
    </s:Group>

    <s:Group id="adminGroup" width="100%" height="100%" includeInLayout="false" visible="false">
        <s:Scroller width="100%" height="100%"
                    creationComplete="onCreationComplete(event)">

            <s:VGroup width="100%">
                <s:HGroup>
                    <s:DropDownList id="accountTypeComboBox"
                                    dataProvider="{Utils.accountTypes}"
                                    labelFunction="{Utils.enumLabelFunction}"
                                    selectedIndex="1"/>

                    <s:TextInput id="accountIdInput"/>

                    <s:Button label="Get" click="onGetClick(event)"/>
                </s:HGroup>

                <s:Spacer height="50"/>

                <s:VGroup id="stateHolder"/>

                <s:Button label="Delete Account" click="onDeleteClick(event)"/>

            </s:VGroup>
        </s:Scroller>
    </s:Group>

    <fx:Script><![CDATA[
        import mx.controls.Alert;
        import mx.events.FlexEvent;

        import protos.AccountId;
        import protos.AccountState;
        import protos.AdminAccountStateEvent;
        import protos.AuthenticatedAsAdminEvent;
        import protos.Item;
        import protos.Skill;
        import protos.Slot;

        import ru.rknrl.castles.events.AddBalanceEvent;
        import ru.rknrl.castles.events.AddMagicItemEvent;
        import ru.rknrl.castles.events.SetSkillEvent;
        import ru.rknrl.castles.events.SetSlotEvent;

        private static const port:int = 2337;
        private static const policyPort:int = 2336;

        private var server:Server;

        private function onCreationComplete(event:FlexEvent):void {
            stateHolder.addEventListener(AddBalanceEvent.ADD_BALANCE, onAddBalance);
            stateHolder.addEventListener(AddMagicItemEvent.ADD_MAGIC_ITEM, onAddMagicItem);
            stateHolder.addEventListener(SetSkillEvent.SET_SKILL, onSetSkill);
            stateHolder.addEventListener(SetSlotEvent.SET_SLOT, onSetSlot);
        }

        private function onConnectClick(event:MouseEvent):void {
            authGroup.includeInLayout = authGroup.visible = false;
            loadingGroup.includeInLayout = loadingGroup.visible = true;
            createConnection(hostTextInput.text, port);
        }

        private function createConnection(host:String, port:int):void {
            if (server) throw new Error("already connected");

            Security.loadPolicyFile("xmlsocket://" + host + ":" + policyPort);

            var socket:Socket = new Socket();
            server = new Server(socket);
            socket.addEventListener(Event.CONNECT, onConnect);
            socket.addEventListener(SecurityErrorEvent.SECURITY_ERROR, onConnectionError);
            socket.addEventListener(IOErrorEvent.IO_ERROR, onConnectionError);
            socket.addEventListener(Event.CLOSE, onConnectionError);
            socket.connect(host, port);
        }

        private function onConnectionError(event:Event):void {
            Alert.show(event.toString());
        }

        private function onConnect(event:Event):void {
            server.addEventListener(AuthenticatedAsAdminEvent.AUTHENTICATED_AS_ADMIN, onAuthenticatedAsAdmin);
            server.adminAuthenticate(loginTextInput.text, passwordTextInput.text);
        }

        public function onAuthenticatedAsAdmin(e:AuthenticatedAsAdminEvent):void {
            server.removeEventListener(AuthenticatedAsAdminEvent.AUTHENTICATED_AS_ADMIN, onAuthenticatedAsAdmin);
            loadingGroup.includeInLayout = loadingGroup.visible = false;
            adminGroup.includeInLayout = adminGroup.visible = true;
            server.addEventListener(AdminAccountStateEvent.ADMIN_ACCOUNT_STATE, onAccountState);
        }

        private function onGetClick(event:MouseEvent):void {
            stateHolder.removeAllElements();

            const accountId:AccountId = new AccountId();
            accountId.id = accountIdInput.text;
            accountId.accountType = accountTypeComboBox.selectedItem;
            server.adminGetAccountState(accountId);
        }

        private var currentAccountId:AccountId;

        public function onAccountState(e:AdminAccountStateEvent):void {
            stateHolder.removeAllElements();

            const state:AccountState = e.accountState;

            currentAccountId = e.accountId;

            const nameRow:NameRow = new NameRow();
            nameRow.init(currentAccountId);
            stateHolder.addElement(nameRow);

            stateHolder.addElement(spacer);

            const goldRow:BalanceRow = new BalanceRow();
            goldRow.init(state.gold);
            stateHolder.addElement(goldRow);

            stateHolder.addElement(spacer);

            for each(var slot:Slot in state.slots) {
                const slotRow:SlotRow = new SlotRow();
                slotRow.init(slot.id, slot.buildingPrototype);
                stateHolder.addElement(slotRow);
            }

            stateHolder.addElement(spacer);

            for each(var skill:Skill in state.skills) {
                const skillRow:SkillRow = new SkillRow();
                skillRow.init(skill.skillType, skill.level);
                stateHolder.addElement(skillRow);
            }

            stateHolder.addElement(spacer);

            for each(var item:Item in state.items) {
                const magicItemRow:MagicItemRow = new MagicItemRow();
                magicItemRow.init(item.itemType, item.count);
                stateHolder.addElement(magicItemRow);
            }
        }

        private static function get spacer():Spacer {
            const spacer:Spacer = new Spacer();
            spacer.height = 16;
            return spacer;
        }

        private function onAddBalance(event:AddBalanceEvent):void {
            const dto:AdminAddGoldDTO = new AdminAddGoldDTO();
            dto.accountId = currentAccountId;
            dto.amount = event.amount;
            server.addGold(dto)
        }

        private function onAddMagicItem(event:AddMagicItemEvent):void {
            const dto:AdminAddItemDTO = new AdminAddItemDTO();
            dto.accountId = currentAccountId;
            dto.itemType = event.itemType;
            dto.amount = event.amount;
            server.addItem(dto)
        }

        private function onSetSlot(event:SetSlotEvent):void {
            const dto:AdminSetSlotDTO = new AdminSetSlotDTO();
            dto.accountId = currentAccountId;
            dto.slot = event.slotDto;
            server.setSlot(dto)
        }

        private function onSetSkill(event:SetSkillEvent):void {
            const dto:AdminSetSkillDTO = new AdminSetSkillDTO();
            dto.accountId = currentAccountId;
            dto.skilType = event.skillType;
            dto.skillLevel = event.skillLevel;
            server.setSkill(dto)
        }

        private function onDeleteClick(event:MouseEvent):void {
            server.deleteAccount(currentAccountId);
        }
        ]]></fx:Script>
</s:Application>
